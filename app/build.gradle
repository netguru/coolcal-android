apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.autoscout24.gradle.todo'

/**
 * Function to retrieve a secret from environment variable or gradle property
 * @param envVarName name of the environment variable to be used in the first place
 * @param propertyName name of the gradle.properties entry
 * @return value of the env. var, property or "undefined"
 */
def secret(String envVarName, String propertyName) {
    try {
        return '"' + (System.getenv(envVarName) ?: property(propertyName)) + '"'
    } catch (MissingPropertyException ignored) {
        return "\"$propertyName undefined\""
    }
}

def hockeyAppAppIdProp = secret("HOCKEYAPP_APP_ID", "HockeyAppAppId")

def openWeatherMapApiKeyProp = secret("OPENWEATHERMAP_API_KEY", "OpenWeatherMapApiKey")

project.ext {
    localBuildVersionCode = Integer.MAX_VALUE
    bitriseBuildNumber = System.getenv("BITRISE_BUILD_NUMBER")
    versionCode = bitriseBuildNumber ? Integer.parseInt(bitriseBuildNumber) : localBuildVersionCode
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "co.netguru.android.coolcal"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode = project.ext.versionCode
        versionName "0.1"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes.each { buildType ->
        buildType.buildConfigField 'String', 'HOCKEYAPP_APP_ID', hockeyAppAppIdProp
        buildType.buildConfigField 'String', 'OPENWEATHERMAP_API_KEY', openWeatherMapApiKeyProp
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug-$versionCode"
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            versionNameSuffix "-release-$versionCode"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        staging {
            applicationId "co.netguru.android.coolcal.staging"
        }

        production {
            applicationId "co.netguru.android.coolcal"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    todo {
        sourceFolder = "app/src/main/kotlin"
        fileExtensions = ["kt", "groovy"]
    }
}

kapt {
    generateStubs = true
}

dependencies {
    // Kotlin standard library
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // HockeyApp
    compile 'net.hockeyapp.android:HockeySDK:3.7.0'

    // Android support libs
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'

    // Play Services Location
    compile 'com.google.android.gms:play-services-location:8.4.0'

    // Sliding up panel
    compile 'com.sothree.slidinguppanel:library:3.2.0'

    // Kotterknife / Butterknife
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    // JodaTime
    compile 'joda-time:joda-time:2.9.1' // todo: change to JodaTime-android for smaller apk

    // SectionCursorAdapter
    compile 'com.twotoasters.SectionCursorAdapter:library:2.0.1'

    // Rebound
    compile 'com.facebook.rebound:rebound:0.3.8'

    // GSON
    compile 'com.google.code.gson:gson:2.5'

    // Retrofit
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'

    // ReactiveX
    compile 'io.reactivex:rxandroid:1.0.1'

    // Dagger2
    kapt 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.google.dagger:dagger:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Picasso
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Anko SQLite
    compile 'org.jetbrains.anko:anko-sqlite:0.8.2'

    // test
    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    // Leak canary
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
}

buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}